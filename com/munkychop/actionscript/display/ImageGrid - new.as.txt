//-----------------------------------------------------------------
//
//ImageGrid by Ivan Hayes
//
//last updated:	30 July 2010 6:53am
//
//-----------------------------------------------------------------


package com.munkychop.actionscript.display
{
	import com.munkychop.actionscript.ui.CustomScrollBar;
	
	import flash.display.MovieClip;
	import flash.display.Sprite;
	import flash.events.Event;
	import flash.geom.ColorTransform;
	
	public class ImageGrid extends MovieClip
	{
		//--------------------------------------------------------------------------------------------
		//--Private Properties
		//--------------------------------------------------------------------------------------------
		private var _scrollBar:CustomScrollBar;
		
		private var _scrollBarAdded:Boolean;
		private var _gridSpacing:int;
		private var _imageGridWidth:int;
		private var _imageGridHeight:int;
		private var _backgroundColour:uint;
		private var _backgroundAlpha:Number;
		private var _gridHolder:MovieClip;
		private var _previousImage:MovieClip;
		private var _backgroundColourSprite:Sprite;
		private var _gridMask:Sprite;
		private var _gridOrientation:String;
		private var _scrollBarAlignment:String;
		
		//--------------------------------------------------------------------------------------------
		//--Constructor
		//--------------------------------------------------------------------------------------------
		public function ImageGrid (imageGridWidth:int, imageGridHeight:int, backgroundColour:uint = 0xFFFFFF, backgroundAlpha:Number = 1, gridOrientation:String = "vertical", scrollBarAlignment:String = "right", gridSpacing:int = 4)
		{
			_imageGridWidth = imageGridWidth;
			_imageGridHeight = imageGridHeight;
			_backgroundColour = backgroundColour;
			_backgroundAlpha = backgroundAlpha;
			_gridOrientation = gridOrientation;
			_scrollBarAlignment = scrollBarAlignment;
			_gridSpacing = gridSpacing;
			
			init ();
		}
		
		//--------------------------------------------------------------------------------------------
		//--Initialization
		//--------------------------------------------------------------------------------------------
		private function init ():void
		{			
			_scrollBarAdded = false;
			
			_gridHolder = new MovieClip ();
			_backgroundColourSprite = new Sprite ();
			_gridMask = new Sprite ();
			
			_backgroundColourSprite.graphics.beginFill (_backgroundColour);
			_backgroundColourSprite.graphics.drawRect (0,0,_imageGridWidth,_imageGridHeight);
			_backgroundColourSprite.graphics.endFill ();
			_backgroundColourSprite.alpha = _backgroundAlpha;
			
			_gridMask.graphics.beginFill (0x000000);
			_gridMask.graphics.drawRect (0,0,_imageGridWidth - _gridSpacing,_imageGridHeight);
			_gridMask.graphics.endFill ();
			
			_gridHolder.cacheAsBitmap = true;
			_gridMask.cacheAsBitmap = true;
			
			_gridHolder.mask = _gridMask;
			
			addChild (_backgroundColourSprite);
			addChild (_gridHolder);
			addChild (_gridMask);
		}
		
		//--------------------------------------------------------------------------------------------
		//--Getter/Setter Methods
		//--------------------------------------------------------------------------------------------
		public function get imageGridWidth ():int
		{
			return _imageGridWidth;
		}
		
		public function set imageGridWidth (imageGridWidth:int):void
		{
			_imageGridWidth = imageGridWidth;
		}
		
		public function get imageGridHeight ():int
		{
			return _imageGridHeight;
		}
		
		public function set imageGridHeight (imageGridHeight:int):void
		{
			_imageGridHeight = imageGridHeight;
		}
		
		public function get draggingScrollBar ():Boolean
		{
			return _scrollBar.dragging;
		}
		
		public function set draggingScrollBar (booleanValue:Boolean):void
		{
			_scrollBar.dragging = booleanValue;
		}
		
		public function get gridMaskWidth ():int
		{
			return _gridMask.width;
		}
		
		public function set gridMaskWidth (newMaskWidth:int):void
		{
			_gridMask.width = newMaskWidth;
		}
		
		public function get gridMaskHeight ():int
		{
			return _gridMask.height;
		}
		
		public function set gridMaskHeight (newMaskHeight:int):void
		{
			_gridMask.height = newMaskHeight;
		}
		
		public function get scrollBarAdded ():Boolean
		{
			return _scrollBarAdded;
		}
		
		public function set scrollBarAdded (newValue:Boolean):void
		{
			_scrollBarAdded = newValue;
		}
		
		
		
		//--------------------------------------------------------------------------------------------
		//--Public Methods
		//--------------------------------------------------------------------------------------------
		public function addImage (image:MovieClip):void
		{
			if (_gridOrientation == "vertical")
			{
				_backgroundColourSprite.height = _imageGridHeight;
				
				if (_previousImage != null)
				{
					image.x = _previousImage.x + _previousImage.width + _gridSpacing;
					image.y = _previousImage.y;
				}
				
				if (image.x + image.width > _imageGridWidth)
				{
					//put image on the next line
					image.x = 0;
					image.y += image.height + _gridSpacing;
				}
				
				if (!_scrollBarAdded && image.y + image.height > _imageGridHeight)
				{
					_scrollBarAdded = true;
					addScrollBar ();
				}
			}
			else if (_gridOrientation == "horizontal")
			{
				_backgroundColourSprite.width = _imageGridWidth;
				
				if (_previousImage != null)
				{
					image.x = _previousImage.x;
					image.y = _previousImage.y + _previousImage.height + _gridSpacing;
				}
				else
				{
					image.x = _gridSpacing;
				}
				
				if (image.y + image.height > _imageGridHeight)
				{
					//puts image on the next line
					image.x += image.width + _gridSpacing;
					image.y = 0;
				}
				
				if (!_scrollBarAdded && image.x + image.width > _imageGridWidth)
				{
					_scrollBarAdded = true;
					addScrollBar ();
				}
			}
			
			_gridHolder.addChild (image);
			
			_previousImage = image;
		}
		
		public function addScrollBar ():void
		{
			if (_gridOrientation == "vertical")
			{
				_scrollBar = new CustomScrollBar (_gridHolder, _imageGridHeight, _imageGridHeight, _gridOrientation);
				
				if (_scrollBarAlignment == "right")
				{
					_scrollBar.x = _gridHolder.x + _gridHolder.width + _gridSpacing;
				}
				else if (_scrollBarAlignment == "left")
				{
					_scrollBar.x = _gridHolder.x - _scrollBar.width - _gridSpacing;
				}
			}
			else if (_gridOrientation == "horizontal")
			{
				_scrollBar = new CustomScrollBar (_gridHolder, _imageGridWidth, _imageGridWidth, _gridOrientation);
				
				if (_scrollBarAlignment == "top")
				{
					_scrollBar.y = _gridHolder.y - _scrollBar.height - _gridSpacing;
				}
				else if (_scrollBarAlignment == "bottom")
				{
					_scrollBar.y = _gridHolder.y + _gridHolder.height + _gridSpacing;
				}
			}
			
			addChild (_scrollBar);
		}
		
		public function removeScrollBar ():void
		{
			_scrollBar.removeAllListeners ();
			removeChild (_scrollBar);
			_scrollBar = null;
			
			_scrollBarAdded = false;
		}
	}
}